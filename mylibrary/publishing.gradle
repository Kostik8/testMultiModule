task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/R.html', '**/R.java', '**/BuildConfig.java'
    options.addStringOption('doclint:none', '-quiet')
    failOnError false
}

task generateJavadocsJar(type: Jar) {
    classifier 'javadoc'
    from generateJavadocs.destinationDir
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.example.konstantin.mylibrary'
            version mylibraryVersion
            artifact "${project.buildDir}/outputs/aar/${project.name}-debug.aar"
            artifact generateJavadocsJar
            artifact sourceJar

            pom.withXml {
                def root = asNode()
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'Proprietary')
                license.appendNode('url', 'N/A')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

generateJavadocsJar.dependsOn generateJavadocs
artifactoryPublish.dependsOn generateJavadocsJar
artifactoryPublish.dependsOn sourceJar

artifactory {
    contextUrl = "${artifactory_url}"
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = "${mylibraryVersion}".endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
            maven = true
        }
        defaults {
            publishArtifacts = true
            publications('aar')
            publishPom = true
            publishIvy = false
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release-local'
            username = "${artifactory_user}" // The resolver user name
            password = "${artifactory_password}" // The resolver password
        }
    }
}
